class C1Driller : DescentDrone replaces Chaingunguy
{
	Default
	{
		Health 90;
		Radius 20;
		Height 26;
		Mass 500;
		Speed 0.4;
		DropItem "Descent_VulcanCannon", 15;
		
		PainChance 64;
		
		AttackSound "VulcanCannon/Fire";
		SeeSound "Driller/C1/See";
		
		DescentEnemy.turnSpeed 3.0;
		DescentEnemy.MoveFriction 0.04;
		DescentDrone.HeightOffs 10;
		DescentEnemy.Aggression 0.50;
		DescentEnemy.ScorePoints 1000;
		DescentEnemy.DodgeChance 0;
		
		DescentDrone.EyeTics 11;
		DescentDrone.EyeSpeed 0.3;
	}
	
	override void SetupWeapons()
	{
		equipWeapon(1, "VulcanBullet", 20, 0, -10, "RedMuzzleFlash", tag:"Vulkan Cannon"); 
	}
	
	States
	{
		Spawn.Look:
			MODL # 10 A_DescentLook(lookFOV:100);
		Loop;
		
		See:
			MODL # 1 A_PlaySeeSound();
		Chase:
			MODL # 3 A_DescentChase(60, 4096, 256, 8);
		Loop;
			
		Attack:
			MODL # 1 A_FireWeapons(1, (0,4,4));
			MODL # 3;
		goto Chase;
			
		Pain:
			MODL # 12 A_DamageRecoil(8);
		Goto Spawn;
		
		Death:
			MODL ################### 1 
			{
				A_DamageRecoil(6);
				RandomExplosions(2, radius);
			}
			TNT1 # 0 A_DropItems();
			MODL # 4 Spawn("EnemyExplosion", pos);
		Stop;
	}
}

// Cloaked variant
// Maybe this'll enforce the same sort of aggressive play that archies do?
class CloakedDriller : C1Driller replaces Archvile 
{
	Default
	{
		Health 150;
		DropItem "Descent_VulcanCannon", 15;
		DropItem "CloakDevice", 5, 1;
		SeeSound "Driller/Cloaked/See";
		
		DescentEnemy.ScorePoints 1200;
	}
	
	override void SetupWeapons()
	{
		super.SetupWeapons();
		
		A_SetRenderStyle(0.22, STYLE_TranslucentStencil);
		SetShade("#C0A0A0A0");
	}
}