class LaserPlatformRobot : DescentDrone
{
	int volleyCooldown;
	double minEngage;
	Property AttackCooldown : volleyCooldown;
	Property MinimumEngageDist : minEngage;

	Default
	{
		Health 80;
		Radius 15;
		Height 15;
		Mass 400;
		Speed 4;
		DropItem "ShieldsBoost", 20;

		PainChance 8;
		
		AttackSound "Drone/Fire/Laser";
		SeeSound "PlatformRobot/Laser/See";
		
		DescentEnemy.turnSpeed 4.0;
		DescentEnemy.MoveFriction 0.08;
		DescentDrone.HeightOffs 10;
		DescentEnemy.Aggression 0.5;
		DescentEnemy.ScorePoints 300;
		DescentEnemy.DodgeChance 20;
		
		LaserPlatformRobot.AttackCooldown 15;
		LaserPlatformRobot.MinimumEngageDist 256;
	}
	
	override void SetupWeapons()
	{
		equipWeapon(1, "PlatformRobotLaser", 20, 0, 2, "BlueMuzzleFlash", tag:"rapidfire laser #1"); 
	}	
	
	States
	{
		Spawn.Look:
			MODL A 10 A_DescentLook(lookFOV:90);
		Loop;
		
		See:
			MODL A 1 A_PlaySeeSound();
		goto Chase;
		
		Chase:
			MODL A 3 A_DescentChase(A_CheckInCooldown() ? 0 : 40, 2048, minEngage, 10);
		Loop;
			
		Attack:
			MODL A 2 A_FireWeapons(1, (0,2,0));
			MODL A 2 A_FireWeapons(1, (0,2,0));
			MODL A 2 A_FireWeapons(1, (0,2,0));
			MODL A 2 A_FireWeapons(1, (0,2,0));
			MODL A 2 A_FireWeapons(1, (0,2,0));
			MODL A 1 A_SetFireCooldown(volleyCooldown);
		goto Chase;
			
		Pain:
			MODL A 4 A_DamageRecoil(3);
		Goto Spawn;
		
		Death:
			MODL AAAAAAAAAAAAAAAAAAA 1 
			{
				A_DamageRecoil(3);
				RandomExplosions(4, radius);
			}
			TNT1 A 0 A_DropItems();
			MODL A 4 Spawn("EnemyExplosion", pos);
		Stop;
	}
}

class PlatformRobotLaser : C2DroneLaser
{
	Default
	{
		DamageFunction (3);
	}
}

class MissilePlatformRobot : LaserPlatformRobot
{
	Default
	{
		Health 80;
		DropItem "Descent_ConMissile", 30, 1;
		DropItem "Descent_ConMissile", 20, 2;
		DropItem "Descent_ConMissile", 10, 3;

		AttackSound "ConcussionMissile/Fire";
		SeeSound "PlatformRobot/Laser/See";
		
		DescentEnemy.turnSpeed 5.0;
		DescentEnemy.Aggression 0.1;
		DescentEnemy.ScorePoints 800;
		
		LaserPlatformRobot.AttackCooldown 15;
	}
	
	override void SetupWeapons()
	{
		equipWeapon(1, "DSCProj_ConMissile", 20, 0, 2, "RedMuzzleFlash", tag:"rapidfire missile launcher #1"); 
	}
	
	States
	{
		Chase:
			MODL A 3 A_DescentChase(A_CheckInCooldown() ? 0 : 40, 2048, minEngage, 10, moveWhileTurning:false);
		Loop;
	
		Attack:
			MODL A 4 A_FireWeapons(1, (0,2,0));
			MODL A 4 A_FireWeapons(1, (0,2,0));
			MODL A 4 A_FireWeapons(1, (0,2,0));
			MODL A 1 A_SetFireCooldown(volleyCooldown);
		goto Chase;
	}
}

class PlatformRobotSpawner : Actor replaces Cacodemon
{
	Default
	{	
		+NOINTERACTION;
	}
	
	override void PostBeginPlay()
	{
		super.postBeginPlay();
		
		double spawnRNG = random[PTRSP_RNG](0,100);
		class<Actor> robot;
		
		if(spawnRNG <= 20)
			robot = "MissilePlatformRobot";
		else
			robot = "LaserPlatformRobot";
		
		if(robot) Spawn(robot, pos);
		Destroy();
	}
}