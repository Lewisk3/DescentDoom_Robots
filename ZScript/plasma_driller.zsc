
class PlasmaDriller : DescentDrone replaces Fatso
{
	Default
	{
		Health 150;
		Radius 30;
		Height 30;
		Mass 400;
		Speed 2;
		DropItem "Descent_PlasmaCannon", 20;
		DropItem "EnergyBoost", 30, 1;
		DropItem "EnergyBoost", 20, 2;
		
		PainChance 64;
		
		AttackSound "PlasmaCannon/Fire";
		SeeSound "PlasmaDriller/See";
		
		DescentEnemy.turnSpeed 6.0;
		DescentEnemy.MoveFriction 0.03;
		DescentDrone.HeightOffs 10;
		DescentEnemy.Aggression 0.30;
		DescentEnemy.ScorePoints 1000;
		DescentEnemy.DodgeChance 30;
		
		DescentDrone.EyeTics 11;
		DescentDrone.EyeSpeed 0.3;
	}
	
	override void SetupWeapons()
	{
		equipWeapon(1, "DSCProj_Plasma", 20, -17, -6, "GreenMuzzleFlash", tag:"plasma cannon #1"); 
		equipWeapon(2, "DSCProj_Plasma", 20,  17, -6, "GreenMuzzleFlash", tag:"plasma cannon #2");
	}
	
	States
	{
		Spawn.Look:
			MODL # 10 A_DescentLook(lookFOV:100);
		Loop;
		
		See:
			MDOL # 1 A_PlaySeeSound();
		Chase:
			MODL # 3 A_DescentChase(50, 1024, 256, 30, moveWhileTurning:false);
		Loop;
			
		Attack:
			MODL # 2 A_FireWeapons(1, (0,2,2), damageOverride:7);
			MODL # 2 A_FireWeapons(2, (0,2,2), damageOverride:7);
			MODL # 2 A_FireWeapons(1, (0,2,2), damageOverride:7);
			MODL # 2 A_FireWeapons(2, (0,2,2), damageOverride:7);
			MODL # 6;
			MODL # 8 A_RandomReFire(50);
		goto Chase;
			
		Pain:
			MODL # 8 A_DamageRecoil(4);
		Goto Spawn;
		
		Death:
			MODL ######################## 1 
			{
				A_DamageRecoil(6);
				RandomExplosions(2, radius);
			}
			TNT1 # 0 A_DropItems();
			MODL # 4 Spawn("EnemyExplosion", pos);
		Stop;
	}
}